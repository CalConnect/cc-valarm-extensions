[[syntax]]
== Extensible syntax for VALARM

<<RFC5545,section=3.6.6>> defines the syntax for "VALARM" components and properties
within them. However, as written, it is hard to extend this by adding, e.g., a new
property common to all types of alarm. Since many of the extensions defined in this
document need to extend the base syntax, an alternative form for the base syntax is
defined here, with the goal of simplifying specification of the extensions.

A "VALARM" calendar component is re-defined by the following notation:

[source%unnumbered]
----
alarmcext  = "BEGIN" ":" "VALARM" CRLF
             alarmprop
             "END" ":" "VALARM" CRLF

alarmprop  = *(

             ; the following are REQUIRED,
             ; but MUST NOT occur more than once

             action / trigger /

             ; one set of action properties MUST be
             ; present and MUST match the action specified
             ; in the ACTION property

             actionprops /

             ; the following is OPTIONAL,
             ; and MAY occur more than once

             x-prop / iana-prop

             )
      
actionprops = audiopropext / disppropext / emailpropext

audiopropext  = *(

                ; 'duration' and 'repeat' are both OPTIONAL,
                ; and MUST NOT occur more than once each,
                ; but if one occurs, so MUST the other
      
                duration / repeat /
      
                ; the following is OPTIONAL,
                ; but MUST NOT occur more than once
      
                attach
      
                )

disppropext = *(

              ; the following are REQUIRED,
              ; but MUST NOT occur more than once
     
              description /
      
              ; 'duration' and 'repeat' are both OPTIONAL,
              ; and MUST NOT occur more than once each,
              ; but if one occurs, so MUST the other
      
              duration / repeat
      
              )

emailpropext = *(

               ; the following are all REQUIRED,
               ; but MUST NOT occur more than once
      
               description / summary /
      
               ; the following is REQUIRED,
               ; and MAY occur more than once
      
               attendee /
      
               ; 'duration' and 'repeat' are both OPTIONAL,
               ; and MUST NOT occur more than once each,
               ; but if one occurs, so MUST the other
      
               duration / repeat
      
               )
----
